apply plugin: 'war'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.8


dependencies {
    providedCompile group:'javax.websocket', name:'javax.websocket-api', version:'1.1'
    providedCompile group:'javax.ws.rs', name:'javax.ws.rs-api', version:'2.0'
    providedCompile group:'javax.json', name:'javax.json-api', version:'1.0'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    providedCompile 'javax.annotation:javax.annotation-api:1.2'
    providedCompile 'org.mongodb:mongo-java-driver:2.12.3'    
    providedCompile 'javax.inject:javax.inject:1'
    providedCompile 'javax.enterprise.concurrent:javax.enterprise.concurrent-api:1.0'  
    providedCompile 'javax.enterprise:cdi-api:1.2'  
    
    compile 'org.twitter4j:twitter4j-core:4.0.4'
    
    compile 'com.restfb:restfb:1.15.0'
    
    compile 'com.google.oauth-client:google-oauth-client:1.20.0'
    compile 'com.google.api-client:google-api-client:1.19.1'
    
    compile group:'io.jsonwebtoken', name:'jjwt', version:'0.5.1'
    
}

task exportLibs(type: Copy) {
  into "$buildDir/libs/"
  from configurations.runtime
  include "*mongo*.jar"
}

build.dependsOn(exportLibs)

// Set the Eclipse facets to use 3.1 of the Dynamic Web Module which requires Java 1.7 (at least)
// Also include JAX-RS and javascript
eclipse.wtp.facet {
    // Clear default facets to work around bug where you get duplicates including wst 2.4
    facets = []
    facet name: 'jst.java', type: 'fixed'
    facet name: 'jst.web', type: 'fixed'
    facet name: 'jst.web', version: '3.1'
    facet name: 'jst.java', version: '1.8'
    facet name: 'jst.jaxrs', version: '2.0'
    facet name: 'wst.jsdt.web', type: 'fixed'
    facet name: 'wst.jsdt.web', version: '1.0'
}

eclipse.project.file {
    // Exclude node resources from validation
    withXml { xmlProvider ->
        Node project = xmlProvider.asNode()
        Node filter = project.appendNode('filteredResources').appendNode('filter')
        filter.appendNode('id', 2889034)
        filter.appendNode('name', 'node_modules')
        filter.appendNode('type', 22)
        Node matcher = filter.appendNode('matcher')
        matcher.appendNode('id', 'org.eclipse.ui.ide.multiFilter')
        matcher.appendNode('arguments', '1.0-name-matches-false-false-*')
    }
}
